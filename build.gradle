plugins {
    id 'java'
}

group = 'org.lukah'
version = '0.1.0'

repositories {
    mavenCentral()
}
def lwjglVersion = "3.3.3"
def os = getOsName()

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-opengl:$lwjglVersion"

    runtimeOnly "org.lwjgl:lwjgl:$lwjglVersion:natives-$os"
    runtimeOnly "org.lwjgl:lwjgl-glfw:$lwjglVersion:natives-$os"
    runtimeOnly "org.lwjgl:lwjgl-opengl:$lwjglVersion:natives-$os"

    implementation "org.joml:joml:1.10.5"
    implementation "net.objecthunter:exp4j:0.4.8"
    implementation 'com.google.code.gson:gson:2.13.1'
}

test {
    useJUnitPlatform()
}

def getOsName() {
    def osName = System.getProperty("os.name").toLowerCase()
    if (osName.contains("windows")) return "windows"
    if (osName.contains("mac")) return "macos"
    if (osName.contains("linux")) return "linux"
    throw new GradleException("Unsupported OS: $osName")
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': 'org.lukah.Visualisation.app.Application'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it)}
    }
}